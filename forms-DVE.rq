PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX lilacorpora: <http://lila-erc.eu/ontologies/lila_corpora/>
PREFIX powla: <http://purl.org/powla/powla.owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX lila: <http://lila-erc.eu/ontologies/lila/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>

# Use the endpoint "corpora"
# Count the occurrences of forms of the lemmas 'dico' and 'facio' in "De Vulgari Eloquentia" of Dante Alighieri.

SELECT ?lowcase_form (count(?form) as ?count)
WHERE {
  service <https://lila-erc.eu/sparql/lemmaBank/query> {
    VALUES ?LemmaValue { 'dico' 'facio' }
      ?lemma ontolex:writtenRep ?LemmaValue
  }
  ?token lila:hasLemma ?lemma ;
         rdfs:label ?form ;
         lilacorpora:hasCitStructure ?citLayer .
  ?citLayer powla:hasDocument ?doc .
  ?doc dc:title 'De Vulgari Eloquentia'
}
group by (lcase(?form) as ?lowcase_form)
order by desc(?count)
limit 50