###############################################
# TITLE: Velez's Dictionary - lemmas with the base "dico"
# DESCRIPTION: Searches for entries in the Velez's dictionary whose lemmas have the lexical base "dico".
###############################################
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX lila: <http://lila-erc.eu/ontologies/lila/>
PREFIX lime: <http://www.w3.org/ns/lemon/lime#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX powla: <http://purl.org/powla/powla.owl#>

# Searches for entries in the Velez's dictionary whose lemmas have the lexical base "dico".

SELECT ?entryLabel ?definition ?baseLabel ?lexsense 
WHERE {
  <http://lila-erc.eu/data/lexicalResources/LatinPortuguese/Velez/Lexicon> lime:entry ?lexentry .
  ?lexentry ontolex:canonicalForm ?lemma ;
            rdfs:label ?entryLabel ;
            ontolex:sense ?lexsense .
  ?lexsense <http://www.w3.org/2004/02/skos#definition> ?definition .
  ?lemma lila:hasBase ?baseID .
  ?baseID rdfs:label ?baseLabel .
  VALUES ?baseID {<http://lila-erc.eu/data/id/base/97>}
} order by ?lexsense

# NOTES
# For changing the lexical base, replace the base URI with the desired one in line 19. (For a comprehensive list of lexical bases, see <http://lila-erc.eu/ontologies/lila/Base>.)