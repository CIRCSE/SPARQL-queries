PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX po: <http://purl.org/ontology/po/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dcterm: <http://purl.org/dc/terms/>
PREFIX powla: <http://purl.org/powla/powla.owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
prefix lilacorpora: <http://lila-erc.eu/ontologies/lila_corpora/>
prefix lila: <http://lila-erc.eu/ontologies/lila/>
prefix ITTBSynFunction: <http://lila-erc.eu/data/corpora/ITTB/id/synFunction/>


# Searches for the occurrences of singular indicative forms of lemmas that belong to the lexical base of either "duco"(base 10) or "eo" (base 12)  
select ?tokenLabel ?doc ?docTitle where{
  VALUES ?corpora {
    <http://lila-erc.eu/data/corpora/ITTB/id/corpus>
  }
  VALUES ?base {
    <http://lila-erc.eu/data/id/base/10> <http://lila-erc.eu/data/id/base/12>
  }

  ?sameBaseLemma lila:hasBase ?base.
  ?token rdf:type powla:Terminal;
         lila:hasLemma ?sameBaseLemma ;
         rdfs:label ?tokenLabel.
  ?morphoAnn oa:hasTarget ?token;
             oa:hasBody <https://universaldependencies.org/la/feat/Number#Sing>, <https://universaldependencies.org/la/feat/Mood#Ind> .
  ?token powla:hasLayer/powla:hasDocument ?doc.
  ?doc dc:title ?docTitle .
  ?token powla:hasLayer/powla:hasDocument/^powla:hasSubDocument ?corpora .
} group by ?tokenLabel ?doc ?docTitle