PREFIX marl: <http://www.gsi.dit.upm.es/ontologies/marl/ns#>
PREFIX lime: <http://www.w3.org/ns/lemon/lime#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX lilaCorpora: <http://lila-erc.eu/ontologies/lila_corpora/>
PREFIX powla: <http://purl.org/powla/powla.owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
prefix lilacorpora: <http://lila-erc.eu/ontologies/lila_corpora/>
prefix lila: <http://lila-erc.eu/ontologies/lila/>
prefix UDSynFunction: <https://universaldependencies.org/u/dep/>
prefix ITTBSynFunction: <http://lila-erc.eu/data/corpora/ITTB/id/synFunction/>

# Chains in the ITTB where a common or proper noun depends as Obj in the PDT formalism and as obj in the UD formalism, on a verb belonging to either synset of "minimize, derogate, belittle, denigrate " or "fight, fight down, oppose, fight back, defend", and is head of an adjective (in the LemmaBank) with positive polarity.

SELECT distinct ?tokenNoun ?tokenAdjLabel ?tokenNounLabel  ?tokenVerbLabel  ?wordnetSys
WHERE {
  VALUES ?corpora {
    <http://lila-erc.eu/data/corpora/ITTB/id/corpus> 
  }
  VALUES ?synFunctionsUD {
    UDSynFunction:obj
  }
  VALUES ?pos {
    <http://lila-erc.eu/ontologies/lila/proper_noun> <http://lila-erc.eu/ontologies/lila/noun>
  }
  VALUES ?synFunctionsPDT {
    ITTBSynFunction:Obj
  }
  VALUES ?wordnetSys {
    <http://wordnet-rdf.princeton.edu/wn30/01091427-v> <http://wordnet-rdf.princeton.edu/wn30/00864475-v>
  }
  VALUES ?liber {
  <http://lila-erc.eu/data/corpora/ITTB/id/citationUnit/005.LB4.>
  }
  
  
  ?corpora powla:hasSubDocument ?doc.
  ?citLayer powla:hasDocument ?doc.
  ?citLayer lilaCorpora:isLayer ?liber.
  ?liber lilaCorpora:hasCitSubUnit*/powla:hasChild ?tokenNoun. 
  
  
  ?tokenNoun powla:hasLayer/powla:hasDocument ?doc.
  ?tokenNoun rdf:type powla:Terminal;
             lila:hasLemma ?lemmaNoun ;
             powla:hasStringValue ?tokenNounLabel.
  

  ?lemmaNoun lila:hasPOS ?pos ;
             rdfs:label ?lemmaNounLabel.
  ?tokenVerb lila:hasLemma ?lemmaVerb ;
             powla:hasStringValue ?tokenVerbLabel.
  ?lemmaVerb lila:hasPOS <http://lila-erc.eu/ontologies/lila/verb> ;
             rdfs:label ?lemmaVerbLabel.
  <http://lila-erc.eu/data/lexicalResources/LatinWordNet/Lexicon> lime:entry ?leWordnet.
  ?leWordnet ontolex:canonicalForm ?lemmaVerb;
             ontolex:evokes ?wordnetSys.
  ?rel rdf:type ?synFunctionsPDT ,lilacorpora:DependencyRel ;
       lilacorpora:hasHead ?tokenVerb ;
       lilacorpora:hasDep ?tokenNoun .
  minus{
    ?rel2 rdf:type ?synFunctionsUD ,lilacorpora:DependencyRel ;
          lilacorpora:hasHead ?tokenVerb ;
          lilacorpora:hasDep ?tokenNoun .
  }
  ?relAdj rdf:type lilacorpora:DependencyRel ;
          lilacorpora:hasHead ?tokenNoun ;
          lilacorpora:hasDep ?adjToken .
  ?adjToken rdf:type powla:Terminal;
            lila:hasLemma ?lemmaAdj;
            powla:hasStringValue ?tokenAdjLabel.
  ?lemmaAdj lila:hasPOS <http://lila-erc.eu/ontologies/lila/adjective>;
            rdfs:label ?lemmaAdjLabel.
  <http://lila-erc.eu/data/lexicalResources/LatinAffectus/Lexicon> lime:entry ?leLemmaAdj.
  ?leLemmaAdj ontolex:canonicalForm ?lemmaAdj;
              ontolex:sense ?latinAffectusSense.
  ?latinAffectusSense marl:hasPolarity marl:Positive.
}




