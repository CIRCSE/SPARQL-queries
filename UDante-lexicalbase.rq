PREFIX powla: <http://purl.org/powla/powla.owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
prefix lilacorpora: <http://lila-erc.eu/ontologies/lila_corpora/>
prefix lila: <http://lila-erc.eu/ontologies/lila/>
prefix ITTBSynFunction: <http://lila-erc.eu/data/corpora/ITTB/id/synFunction/>

# Given a lexical base (loquor), find all the lemmas of that lexical base in Dante and ITTB
SELECT ?sameBaseLemmaLabel ?docTitle
WHERE {
  VALUES ?copora {
    <http://lila-erc.eu/data/corpora/ITTB/id/corpus> <http://lila-erc.eu/data/corpora/UDante/id/corpus>
  }
  <http://lila-erc.eu/data/id/lemma/110805> lila:hasBase ?base .
  ?sameBaseLemma lila:hasBase ?base.
  ?token rdf:type powla:Terminal;
         lila:hasLemma ?sameBaseLemma .
  ?token powla:hasLayer/powla:hasDocument ?doc.
  ?doc dc:title ?docTitle .
  ?token powla:hasLayer/powla:hasDocument/^powla:hasSubDocument ?copora .
  ?sameBaseLemma rdfs:label ?sameBaseLemmaLabel.
}group by ?sameBaseLemma ?sameBaseLemmaLabel ?docTitle
order by ?sameBaseLemma ?docTitle

