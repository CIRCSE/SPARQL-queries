prefix ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX powla: <http://purl.org/powla/powla.owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX lilaCorpus: <http://lila-erc.eu/ontologies/lila_corpora/>
PREFIX lemonEty: <http://lari-datasets.ilc.cnr.it/lemonEty#>
PREFIX lime: <http://www.w3.org/ns/lemon/lime#>
prefix lila: <http://lila-erc.eu/ontologies/lila/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

# Find the lemmas with a proto-italic reconstructed forms in the "Monarchia" of Dante Alighieri
SELECT  ?lemma ?wrs ?pie
{
  {
    SELECT ?lemma (GROUP_CONCAT(?wr ;separator=", ") as ?wrs ) ?pie {
      ?le a ontolex:LexicalEntry ;
  ontolex:canonicalForm ?lemma ;
  lemonEty:etymology ?e .
      ?e lemonEty:etymon ?pieety .
      ?pieety lime:language "PIt" ;
              rdfs:label ?pie .
      ?lemma ontolex:writtenRep ?wr
    }group by ?lemma ?pie
  }
  ?tokenURI lila:hasLemma ?lemma .
  ?tokenURI powla:hasLayer ?docLayer .
  ?docLayer powla:hasDocument ?doc .
  ?doc dc:title "De Monarchia".
}
group by ?lemma ?wrs ?pie

