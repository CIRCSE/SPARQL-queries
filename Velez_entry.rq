PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX lila: <http://lila-erc.eu/ontologies/lila/>
PREFIX lime: <http://www.w3.org/ns/lemon/lime#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>

# Searches for entries in the Velez's dictionary whose lemmas have a certain written representation (e.g. "sero").

SELECT ?label ?partofspeech ?inflectiontype ?definition ?lemma
WHERE {
  <http://lila-erc.eu/data/lexicalResources/LatinPortuguese/Velez/Lexicon> lime:entry ?lexentry .
  ?lexentry ontolex:canonicalForm ?lemma ;
            ontolex:sense ?senseURI ;
            rdfs:label ?label .
  ?lemma lila:hasInflectionType ?fl_cat ;
         lila:hasPOS ?UPOS.
  ?UPOS rdfs:label ?partofspeech .
  ?fl_cat rdfs:label ?inflectiontype .
  ?senseURI rdfs:label ?definition .
  ?lemma ontolex:writtenRep ?lemmaWrittenRep .
  FILTER regex(?lemmaWrittenRep, "^sero$")
} order by ?senseURI

# NOTE
# For changing the lemma, replace 'sero' with the desired one in line 23 ("^__$").