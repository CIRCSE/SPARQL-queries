PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX lilacorpora: <http://lila-erc.eu/ontologies/lila_corpora/>
PREFIX powla: <http://purl.org/powla/powla.owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX lila: <http://lila-erc.eu/ontologies/lila/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX literal: <http://www.essepuntato.it/2010/06/literalreification/>
prefix marl: <http://www.gsi.dit.upm.es/ontologies/marl/ns#>
prefix premon: <http://premon.fbk.eu/ontology/core#>
prefix lv: <http://lila-erc.eu/ontologies/latinVallex/>

# Find forms that have a specific synset in the currently revised portion of Latin WordNet (e.g. 00179456-v: "take out or remove") and a valency frame in Latin Vallex with specific functor (e.g., ADDR "addressee")
SELECT ?form (count(?form) as ?count) ?docTitle ?prefixlabel
WHERE {
  ?le ontolex:canonicalForm ?lemma ;
      ontolex:evokes ?frame ;
      ontolex:evokes <http://lila-erc.eu/data/lexicalResources/LatinWordNet/id/LexicalConcept/00179456-v> .
  ?frame premon:semRole ?SemRole .
  ?SemRole lv:functor <http://lila-erc.eu/ontologies/latinVallex/ADDR> .
  ?lemma lila:hasPrefix ?prefix .
  ?prefix rdfs:label ?prefixlabel .
  ?token lila:hasLemma ?lemma ;
         rdfs:label ?form ;
         powla:hasLayer ?docLayer .
  ?docLayer powla:hasDocument ?doc .
  ?doc dc:title ?docTitle
}
group by ?form ?docTitle ?prefixlabel
order by desc(?count)
