PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX lila: <http://lila-erc.eu/ontologies/lila/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX powla: <http://purl.org/powla/powla.owl#>
PREFIX prefix: <http://lila-erc.eu/data/id/prefix/>

#Lists all the tokens of the various documents of the CLaSSES corpus that belong to a lemma featuring a specific affix in its derivational history (in the example: prefix n. 5 "ad-")


SELECT ?tokenClasses ?tokenClassesLabel ?titlesubDocLayer ?affixLabel
WHERE {
  	?lemmaLiLa a lila:Lemma ;
  			lila:hasPrefix prefix:5 .
  	prefix:5 rdfs:label ?affixLabel .
	?tokenClasses lila:hasLemma ?lemmaLiLa ;
			powla:hasLayer ?docLayer ;
   			rdfs:label ?tokenClassesLabel .
	?docLayer powla:hasDocument ?subDocLayer .
	?subDocLayer dc:title ?titlesubDocLayer .
  	<http://lila-erc.eu/data/corpora/CLaSSES/id/corpus> powla:hasSubDocument ?subDocLayer .
} 

