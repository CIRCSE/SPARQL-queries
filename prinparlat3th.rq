PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX lime: <http://www.w3.org/ns/lemon/lime#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX lila: <http://lila-erc.eu/ontologies/lila/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX powla: <http://purl.org/powla/powla.owl#>

# counts the number of distinct lemmas in LASLA for each inflection micro-class of verbs of the third conjugation.

SELECT  ?inflClass ?inflClassDesc  (count(?inflClass) as ?nLemmas)  WHERE {
  <http://lila-erc.eu/data/lexicalResources/prinparlat/Lexicon> lime:entry ?flexeme.
  ?flexeme ontolex:canonicalForm ?lemma;
           <https://www.paralex-standard.org/paralex_ontology.xml#inflection_class> ?inflClass;
           lila:hasInflectionType ?iType.
  ?iType rdf:type <http://lila-erc.eu/ontologies/lila/ThirdConjugation>.
  ?inflClass dcterms:description ?inflClassDesc.
  {
    Select ?lemma where {
      VALUES ?corpus {
        <http://lila-erc.eu/data/corpora/Lasla/id/corpus>
      }
      ?corpus powla:hasSubDocument ?doc.
      ?docLayer powla:hasDocument ?doc.
      ?token powla:hasLayer ?docLayer;
             lila:hasLemma ?lemma;
             powla:hasStringValue ?tokLabel.
    } group by ?lemma
  }
} group by  ?inflClass ?inflClassDesc
order by desc (?nLemmas)
