PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX powla: <http://purl.org/powla/powla.owl#>
PREFIX lila: <http://lila-erc.eu/ontologies/lila/>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX lime: <http://www.w3.org/ns/lemon/lime#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

#Selects the lexical entry of the Lexicala Latin-French Dictionary whose definitions include the word "enlever"
#Selects the corresponding lemmas in the Lemma Bank that are formed with either the prefix "de-" or the prefix "a(b)-"
#Searches for the tokens of the selected lemma in 4 corpora: CIRCSE Latin Library, LASLA, Fibonacci, UDante

SELECT ?token_label (count(?token) as ?nToken) ?corpora_title
WHERE {
  VALUES ?prefixes {
   	<http://lila-erc.eu/data/id/prefix/21> #de-
    <http://lila-erc.eu/data/id/prefix/1> #a(b)-
  }
  VALUES ?corpora {
    <http://lila-erc.eu/data/corpora/CIRCSELatinLibrary/id/corpus> 
    <http://lila-erc.eu/data/corpora/UDante/id/corpus> 
    <http://lila-erc.eu/data/corpora/Lasla/id/corpus> 
    <http://lila-erc.eu/data/corpora/CorpusFibonacci/id/corpus> 
    <http://lila-erc.eu/data/corpora/CLaSSES/id/corpus>
  }
	<http://lila-erc.eu/data/lexicalResources/Lexicala/Lexicon/Lexicon> lime:entry ?lexicala_entry .
	?lexicala_entry ontolex:sense ?lexicala_sense ;
                 	ontolex:canonicalForm ?lemma_lemmaBank .
	?lexicala_sense <http://www.w3.org/2004/02/skos#definition> ?lexicala_definition .
  	?lemma_lemmaBank lila:hasPrefix ?prefixes .
	filter(regex(?lexicala_definition, "enlever"))
  	
	?token lila:hasLemma ?lemma_lemmaBank ;
           rdf:type powla:Terminal ;
           powla:hasLayer ?DocumentLayer ;
		   rdfs:label ?token_label .
  	?DocumentLayer powla:hasDocument ?Document .
  	?Document ^powla:hasSubDocument ?corpora .
  	?corpora dc:title ?corpora_title .
}
order by desc(?nToken) ?token_label ?corpora_title
