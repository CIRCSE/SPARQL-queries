PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX lilacorpora: <http://lila-erc.eu/ontologies/lila_corpora/>
PREFIX powla: <http://purl.org/powla/powla.owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX lila: <http://lila-erc.eu/ontologies/lila/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>

# Count the occurrences of forms of the lemmas 'dico' and 'facio' in Dante's "De Vulgari Eloquentia" and Seneca's "De Otio".

SELECT ?lowcase_form (count(?form) as ?count) ?texts
WHERE {
  VALUES ?LemmaValue {
    'dico' 'facio' 
  }
  VALUES ?texts {
    'De Vulgari Eloquentia' 'De Otio'
  }
  ?lemma ontolex:writtenRep ?LemmaValue.
  ?token lila:hasLemma ?lemma ;
         rdfs:label ?form ;
         powla:hasLayer ?docLayer .
  ?docLayer powla:hasDocument ?doc .
  ?doc dc:title ?texts
}
group by (lcase(?form) as ?lowcase_form) ?texts
order by?lowcase_form

